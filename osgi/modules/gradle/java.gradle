allprojects {
	repositories {
		mavenCentral()
	}
}

subprojects {
	apply from: "../../gradle/baseline.gradle"
	apply plugin: "java"

	sourceCompatibility = 1.6
	targetCompatibility = 1.6

	ext.liferayProjectDir = "${rootDir}/../../.."

	dependencies {
		compile files(["${liferayProjectDir}/portal-service/classes"])
		compile files(["${liferayProjectDir}/util-java/classes"])

		compile fileTree(
			dir:"${liferayProjectDir}/lib/development",
			includes: ["**/*.jar"]
		)

		compile fileTree(
			dir:"${liferayProjectDir}/lib/global",
			includes: ["**/*.jar"]
		)

		compile fileTree(
			dir:"${liferayProjectDir}/lib/portal",
			includes: ["**/*.jar"]
		)
	}

	jar {
		actions = []

		doLast {
			tasks.baseline.execute()
		}
	}

	task clean << {
	}

	task deploy(dependsOn: jar) << {
		if (project.hasProperty('deployDir') && (project.deployDir != null)) {
			def buildLibDir = new File("${projectDir}/build/libs")

			logger.info "COPY ${buildLibDir}/${project.name}.jar TO " + project.deployDir

			copy {
				from "${buildLibDir}/${project.name}.jar"
				into project.deployDir
			}
		}
		else {
			logger.quiet "[NOTE] Skipping deploy because ${project.name}'s 'deployDir' property is not set."
			logger.quiet "[NOTE] First, see if it's build.gradle file contains the property 'project.ext.deployDir'."
			logger.quiet "[NOTE] Second, make sure to execute the deploy from 'ant'."
		}
	}
}