import groovy.json.JsonBuilder

apply plugin: "base"
apply plugin: "com.liferay.gulp"

buildDir = file("build_gradle")

task createLiferayThemeJson

clean {
	dependsOn cleanNpmInstall
	dependsOn cleanCreateLiferayThemeJson

	delete "build"
	delete "dist"
}

createLiferayThemeJson {
	File liferayThemeJsonFile = file("liferay-theme.json")

	doLast {
		JsonBuilder jsonBuilder = new JsonBuilder()

		File appServerTomcatDir = file(project.properties["app.server.tomcat.dir"])
		File appServerThemeDir = new File(project.properties['app.server.tomcat.deploy.dir'], project.name)
		File deployDir = new File(project.properties["liferay.home"], "deploy")

		jsonBuilder.LiferayTheme {
			appServerPath appServerTomcatDir.absolutePath
			appServerPathTheme appServerThemeDir.absolutePath
			deployed false
			deployPath deployDir.absolutePath
			themeName project.name
		}

		liferayThemeJsonFile.text = jsonBuilder
	}

	onlyIf {
		!liferayThemeJsonFile.exists()
	}

	outputs.file liferayThemeJsonFile
}

tasks.all {
	if (it.class.name.startsWith("com.liferay.gradle.plugins.gulp.ExecuteGulpTask")) {
		it.dependsOn createLiferayThemeJson, npmInstall
	}
}