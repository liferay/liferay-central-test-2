import com.liferay.gradle.util.copy.ReplaceLeadingPathAction

String pluginsSDKVersion = project.properties["plugins.sdk.version"]

configurations {
	pluginsSDK {
		transitive = false
	}
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.plugins", version: "3.3.29"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.app.javadoc.builder", version: "1.2.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.baseline", version: "1.1.7"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.cache", version: "1.0.12"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.change.log.builder", version: "1.0.3"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.dependency.checker", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.patcher", version: "1.0.12"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.whip", version: "1.0.4"
	compile group: "com.netflix.nebula", name: "gradle-extra-configurations-plugin", version: "3.0.3"
	compile group: "org.apache.aries.jmx", name: "org.apache.aries.jmx.api", version: "1.1.0"

	compileOnly fileTree(builtBy: [rootProject.tasks.extractGradleApi214], dir: new File(rootProject.buildDir, "gradle-2.14"))

	pluginsSDK group: "com.liferay.portal", name: "com.liferay.portal.plugins.sdk", version: pluginsSDKVersion
}

processResources {
	String destinationDirName = "com/liferay/gradle/plugins/defaults/dependencies"
	String zipDirName = "com.liferay.portal.plugins.sdk-${pluginsSDKVersion}/dependencies/net.sourceforge.pmd/rulesets/java"

	into(destinationDirName) {
		eachFile new ReplaceLeadingPathAction("${destinationDirName}/${zipDirName}": destinationDirName)

		from {
			zipTree(configurations.pluginsSDK.singleFile)
		}

		include "${zipDirName}/standard-rules.xml"
		includeEmptyDirs = false
	}
}