subprojects {
	apply plugin: "java"

	group = "com.example.sample"

	repositories {
		mavenLocal()

		maven {
			url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
		}
	}
}

task runGradleTest

project(":sample-api") {
	dependencies {
		compile group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
		compile group: "com.liferay", name: "com.liferay.osgi.util", version: "3.0.0"
		compile group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.6.0"
		compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	}
}

project(":sample-service") {
	apply plugin: "com.liferay.portal.tools.service.builder"

	buildService {
		apiDir = "../sample-api/src/main/java"
		osgiModule = true
	}

	dependencies {
		compile group: "com.liferay", name: "com.liferay.portal.spring.extender", version: "2.0.0"
		compile project(":sample-api")
	}
}

runGradleTest {
	Project serviceProject = project(":sample-service")

	dependsOn serviceProject.tasks.buildService

	doLast {
		Project apiProject = project(":sample-api")

		String[] fileNames = [
			"src/main/java/com/example/sample/exception/NoSuchFooException.java",
			"src/main/java/com/example/sample/model/Foo.java",
			"src/main/java/com/example/sample/service/FooLocalService.java"]

		for (String fileName in fileNames) {
			File file = apiProject.file(fileName)

			assert file.exists()
		}

		fileNames = [
			"src/main/java/com/example/sample/model/impl/FooImpl.java",
			"src/main/java/com/example/sample/service/impl/FooLocalServiceImpl.java",
			"src/main/java/com/example/sample/service/util/ServiceProps.java",
			"src/main/resources/META-INF/sql/tables.sql",
			"src/main/resources/service.properties"]

		for (String fileName in fileNames) {
			File file = serviceProject.file(fileName)

			assert file.exists()
		}
	}
}