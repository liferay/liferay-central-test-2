import aQute.bnd.osgi.Constants

import org.gradle.util.VersionNumber

buildscript {
	dependencies {
		String gradleBundlePluginVersion = "0.8.6"

		VersionNumber gradleVersionNumber = VersionNumber.parse(gradle.gradleVersion)

		if (gradleVersionNumber <= new VersionNumber(2, 13, 0, null)) {
			gradleBundlePluginVersion = "0.8.4"
		}

		classpath group: "org.dm.gradle", name: "gradle-bundle-plugin", version: gradleBundlePluginVersion
	}

	repositories {
		maven {
			url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
		}
	}
}

task runGradleTest

project(":sample-100") {
	apply plugin: "maven"
}

project(":sample-101") {
	apply plugin: "com.liferay.baseline"

	baseline {
		dependsOn ":sample-100:install"
		ignoreFailures = true
	}

	baselineConfiguration {
		allowMavenLocal = true
	}
}

runGradleTest {
	Project sample101Project = project(":sample-101")

	dependsOn sample101Project.tasks.baseline

	doLast {
		Properties bndProperties = _getBndProperties(sample101Project)

		assert bndProperties[Constants.BUNDLE_VERSION] == "2.0.0"

		File packageInfoFile = sample101Project.file("src/main/resources/com/example/sample/packageinfo")

		assert packageInfoFile.exists()
		assert packageInfoFile.text == "version 2.0.0"
	}
}

subprojects {
	apply plugin: "java"
	apply plugin: "org.dm.bundle"

	Properties bndProperties = _getBndProperties(project)

	archivesBaseName = bndProperties[Constants.BUNDLE_SYMBOLICNAME]
	group = "com.example"
	version = bndProperties[Constants.BUNDLE_VERSION]

	bundle {
		instruction "-include", "bnd.bnd"
	}

	repositories {
		mavenLocal()
	}
}

private Properties _getBndProperties(Project project) {
	Properties bndProperties = new Properties()

	File bndFile = project.file("bnd.bnd")

	bndFile.withInputStream {
		bndProperties.load it
	}

	return bndProperties
}