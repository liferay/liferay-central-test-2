import com.liferay.gradle.util.copy.StripPathSegmentsAction

import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"
apply plugin: "org.ysb33r.gradletest"

task copyDistBundleZipPluginsSdk(type: Copy)
task downloadPluginsSdk(type: Download)
task copyGradleTestDependencies(type: Copy)

String bundleUrl = "https://cdn.lfrs.sl/releases.liferay.com/portal/7.0.2-ga3/liferay-ce-portal-tomcat-7.0-ga3-20160804222206210.zip"
String pluginsSdkUrl = "https://cdn.lfrs.sl/releases.liferay.com/portal/7.0.2-ga3/com.liferay.portal.plugins.sdk-7.0-ga3-20160804222206210.zip"

if (System.properties["http.proxyHost"] == "squid.lax.liferay.com") {
	bundleUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/7.0.2-ga3/liferay-ce-portal-tomcat-7.0-ga3-20160804222206210.zip"
	pluginsSdkUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/7.0.2-ga3/com.liferay.portal.plugins.sdk-7.0-ga3-20160804222206210.zip"
}

copyDistBundleZipPluginsSdk {
	dependsOn downloadPluginsSdk
	eachFile new StripPathSegmentsAction(1)

	from {
		zipTree(downloadPluginsSdk.dest)
	}

	includeEmptyDirs = false
	into "src/gradleTest/distBundleZip/plugins-sdk"
}

downloadPluginsSdk {
	dest new File(buildDir, "plugins-sdk.zip")
	src pluginsSdkUrl
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.plugins", version: "3.3.31"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "1.0.11"
	compile group: "de.undercouch", name: "gradle-download-task", version: "3.2.0"

	compileOnly fileTree(builtBy: [rootProject.tasks.extractGradleApi214], dir: new File(rootProject.buildDir, "gradle-2.14"))
}

gradleTest {
	dependsOn copyDistBundleZipPluginsSdk
	dependsOn copyGradleTestDependencies
	dependsOn jar

	gradleArguments "--project-prop", "liferay.workspace.bundle.url=${bundleUrl}"
	gradleArguments "--project-prop", "pluginClasspathDir=${uri(jar.destinationDir)}"

	versions "2.14.1", "3.0", "3.1", "3.2.1", "3.3"
}