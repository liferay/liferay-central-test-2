import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

apply plugin: "org.ysb33r.gradletest"

task copyDistBundleZipPluginsSdk(type: Copy)
task copyGradleTestDependencies(type: Copy)

copyDistBundleZipPluginsSdk {
	eachFile new StripPathSegmentsAction(1)

	from {
		zipTree(FileUtil.get(project, "http://mirrors.liferay.com/releases.liferay.com/portal/7.0.2-ga3/com.liferay.portal.plugins.sdk-7.0-ga3-20160804222206210.zip"))
	}

	includeEmptyDirs = false
	into "src/gradleTest/distBundleZip/plugins-sdk"
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.plugins", version: "3.0.50"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "1.0.11"
	compile group: "de.undercouch", name: "gradle-download-task", version: "3.1.1"

	compileOnly fileTree(builtBy: [rootProject.tasks.extractGradleApi214], dir: new File(rootProject.buildDir, "gradle-2.14"))
}

gradleTest {
	dependsOn copyDistBundleZipPluginsSdk
	dependsOn copyGradleTestDependencies
	dependsOn jar

	gradleArguments "--project-prop", "liferay.workspace.bundle.url=http://mirrors.liferay.com/releases.liferay.com/portal/7.0.2-ga3/liferay-ce-portal-tomcat-7.0-ga3-20160804222206210.zip"
	gradleArguments "--project-prop", "pluginClasspathDir=${uri(jar.destinationDir)}"

	versions "2.14.1", "3.0", "3.1", "3.2.1"
}