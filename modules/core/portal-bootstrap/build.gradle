import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.RenameDependencyClosure

import org.dm.gradle.plugins.bundle.JarBuilder

ext {
	systemPackagesExtraManifestHeaders = [
		"Bnd-LastModified": "1468904359940",
		"Created-By": "1.8.0_92 (Oracle Corporation)"
	]
}

task buildSystemPackagesExtraManifest
task copySystemPackagesExtraLibs(type: Copy)
task copySystemPackagesExtraManifest(type: Copy)

String buildProfile = System.properties["build.profile"]
File systemPackagesExtraBndFile = file("system.packages.extra.bnd")
File systemPackagesExtraManifestFile = file("system.packages.extra.mf")

buildSystemPackagesExtraManifest {
	dependsOn classes
	dependsOn copySystemPackagesExtraLibs

	doLast {
		JarBuilder jarBuilder = bundle.jarBuilderFactory.create()

		jarBuilder.withBase projectDir

		Map<String, String> properties = liferayOSGi.bundleDefaultInstructions

		properties["-include"] = relativePath(systemPackagesExtraBndFile)

		jarBuilder.withClasspath new File[0]
		jarBuilder.withName "system.packages.extra"
		jarBuilder.withProperties properties
		jarBuilder.withResources new File[0]
		jarBuilder.withSourcepath sourceSets.main.allSource.srcDirs
		jarBuilder.withTrace false
		jarBuilder.withVersion project.version

		ByteArrayOutputStream byteArrayOutputStream = new ByteArrayOutputStream()

		jarBuilder.writeManifestTo byteArrayOutputStream

		String systemPackagesExtraManifest = byteArrayOutputStream.toString("UTF-8")

		for (entry in systemPackagesExtraManifestHeaders) {
			systemPackagesExtraManifest = systemPackagesExtraManifest.replaceFirst "${entry.key}: .+", "${entry.key}: ${entry.value}"
		}

		systemPackagesExtraManifestFile.setText systemPackagesExtraManifest, "UTF-8"
	}
}

copySystemPackagesExtraLibs {
	from configurations.provided
	into "lib"
	rename new RenameDependencyClosure(project, configurations.provided.name)
}

copySystemPackagesExtraManifest {
	if ((buildProfile == "slim") || !FileUtil.isUpToDate(project, systemPackagesExtraBndFile, systemPackagesExtraManifestFile)) {
		dependsOn buildSystemPackagesExtraManifest
	}

	from systemPackagesExtraManifestFile
	into "tmp/META-INF"

	rename {
		"MANIFEST.MF"
	}
}

dependencies {
	provided group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	provided group: "com.liferay", name: "com.liferay.osgi.service.tracker.collections", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	provided group: "org.osgi", name: "org.osgi.core", version: "6.0.0"
	provided group: "org.springframework", name: "spring-beans", version: "4.1.9.RELEASE"
	provided group: "org.springframework", name: "spring-context", version: "4.1.9.RELEASE"
	provided group: "org.springframework", name: "spring-core", version: "4.1.9.RELEASE"
	provided project(":core:jaxws-osgi-bridge")
	provided project(":core:osgi-felix-util")
	provided project(":core:portal-app-license-api")
	provided project(":core:registry-api")
	provided project(":core:registry-impl")
}

if (buildProfile != "slim") {
	dependencies {
		provided project(":core:portal-compat:blogs:portal-impl-blogs-compat")
		provided project(":core:portal-compat:blogs:portal-kernel-blogs-compat")
	}
}

jar {
	dependsOn copySystemPackagesExtraManifest
}

liferay {
	deployDir = file("${liferayHome}/osgi/core")
}