import com.liferay.gradle.util.FileUtil

import org.dm.gradle.plugins.bundle.JarBuilder

task buildSystemPackagesExtraManifest
task copySystemPackagesExtraManifest(type: Copy)

File systemPackagesExtraBndFile = file("system.packages.extra.bnd")
File systemPackagesExtraManifestFile = file("system.packages.extra.mf")

buildSystemPackagesExtraManifest {
	dependsOn classes
	dependsOn copyLibs

	doLast {
		JarBuilder jarBuilder = bundle.jarBuilderFactory.create()

		jarBuilder.withBase projectDir

		Map<String, String> properties = liferay.bundleDefaultInstructions

		properties["-include"] = relativePath(systemPackagesExtraBndFile)

		jarBuilder.withClasspath new File[0]
		jarBuilder.withName "system.packages.extra"
		jarBuilder.withProperties properties
		jarBuilder.withResources new File[0]
		jarBuilder.withSourcepath sourceSets.main.allSource.srcDirs
		jarBuilder.withTrace false
		jarBuilder.withVersion liferay.portalVersion.replaceAll("-", ".")

		systemPackagesExtraManifestFile.withOutputStream {
			jarBuilder.writeManifestTo it
		}
	}
}

copySystemPackagesExtraManifest {
	if (!FileUtil.isUpToDate(project, systemPackagesExtraBndFile, systemPackagesExtraManifestFile)) {
		dependsOn buildSystemPackagesExtraManifest
	}

	from systemPackagesExtraManifestFile
	into "tmp/META-INF"

	rename {
		"MANIFEST.MF"
	}
}

dependencies {
	// @formatter:off
	compile group: "com.liferay.portal", name: "portal-service", version: liferay.portalVersion
	compile group: "com.liferay.portal", name: "portal-impl", version: liferay.portalVersion
	// @formatter:on
	compile group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compile group: "org.springframework", name: "spring-beans", version: "3.2.15.RELEASE"
	compile group: "org.springframework", name: "spring-context", version: "3.2.15.RELEASE"
	compile project(":core:jaxws-osgi-bridge")
	compile project(":core:osgi-service-tracker-collections")
	compile project(":core:portal-app-license-api")
	compile project(":core:registry-impl")
}

deploy {
	from("lib") {
		include "com.liferay.osgi.servicetrackermap.jar"
		include "org.eclipse.osgi.jar"
		include "registry-impl.jar"
	}
}

jar {
	dependsOn copySystemPackagesExtraManifest
}

liferay {
	deployDir = file("${liferayHome}/osgi/core")
}