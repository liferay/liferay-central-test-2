import com.liferay.gradle.util.StringUtil

task deployFiles(type: Copy)

dependencies {
	testCompile group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
}

deploy {
	deleteAllActions()

	dependsOn = []
	finalizedBy deployFiles
}

deployFiles {
	from processResources

	into {
		liferay.appServerPortalDir
	}
}

Properties importedFilesProperties = new Properties()

File importedFilesPropertiesFile = file("imported-files.properties")

importedFilesPropertiesFile.withInputStream {
	importedFilesProperties.load it
}

importedFilesProperties.each {
	String dependencyNotation, String packageNames ->

	String dependencySuffix = dependencyNotation.replaceAll(/[\.|:](\w)/) {
		String s = it[1]

		s.toUpperCase()
	}

	Configuration dependencyConfiguration = configurations.create("importedFiles" + StringUtil.capitalize(dependencySuffix))

	dependencyConfiguration.transitive = false

	dependencies.add dependencyConfiguration.name, dependencyNotation

	String[] dependencyIncludes = packageNames.tokenize(",")

	processResources {
		from {
			zipTree(dependencyConfiguration.singleFile)
		} {
			include dependencyIncludes
		}
	}
}