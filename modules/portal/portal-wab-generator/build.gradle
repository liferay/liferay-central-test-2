import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.OSDetector

task downloadDependencyVisitor(type: Exec)

String dependencyVisitorUrl = "svn://svn.forge.objectweb.org/svnroot/asm/tags/ASM_5_0_1/examples/dependencies/src/org/objectweb/asm/depend/DependencyVisitor.java"

compileJava {
	dependsOn downloadDependencyVisitor
}

dependencies {
	compile group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "2.4.1"
	compile group: "com.liferay.portal", name: "portal-impl", version: liferay.portalVersion
	compile group: "com.liferay.portal", name: "portal-service", version: liferay.portalVersion
	compile group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compile group: "org.apache.ant", name: "ant", version: "1.9.4"
	compile group: "org.apache.felix", name: "org.apache.felix.fileinstall", transitive: false, version: "3.5.1-SNAPSHOT"
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	compile group: "org.ow2.asm", name: "asm-debug-all", version: "5.0.1"
	compile group: "org.springframework", name: "spring-web", version: "3.2.10.RELEASE"

	provided group: "com.liferay.portal", name: "util-java", version: liferay.portalVersion
}

downloadDependencyVisitor {
	File dependencyVisitorDir = file("src/main/java/org/objectweb/asm/depend")

	if (OSDetector.windows) {
		executable "cmd.exe"

		args "/c"
	}
	else {
		executable "/bin/sh"

		args "-c"
	}

	args "\"svn export ${dependencyVisitorUrl} ${dependencyVisitorDir}\""

	doFirst {
		dependencyVisitorDir.mkdirs()
	}

	doLast {
		ant.patch(dir: "src/main/java", patchfile: "patches/LPS-47293.patch", strip: 1)
	}

	onlyIf {
		File dependencyVisitorFile = new File(dependencyVisitorDir, "DependencyVisitor.java")

		!dependencyVisitorFile.exists()
	}

	workingDir = file("src/main/java")
}

liferay {
	deployDir = file("${liferayHome}/osgi/portal")
}