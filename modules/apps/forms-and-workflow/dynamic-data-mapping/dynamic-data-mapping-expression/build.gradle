sourceCompatibility = "1.8"
targetCompatibility = "1.8"

import com.liferay.gradle.util.FileUtil

import org.apache.tools.ant.filters.FixCrLfFilter

apply plugin: 'antlr'

String parserDirName = "com/liferay/dynamic/data/mapping/expression/internal/parser"

cleanGenerateGrammarSource {
	delete "src/main/java/" + parserDirName
	delete "src/main/resources/" + parserDirName
}

compileJava {
	dependsOn.remove "generateGrammarSource"
}

dependencies {
	antlr group: "org.antlr", name: "antlr4", version: "4.3"

	provided group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	provided group: "com.liferay", name: "com.liferay.osgi.service.tracker.collections", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	provided group: "org.abego.treelayout", name: "org.abego.treelayout.core", version: "1.0.1"
	provided group: "org.antlr", name: "antlr4", version: "4.3"
	provided group: "org.antlr", name: "antlr4-annotations", version: "4.3"
	provided group: "org.antlr", name: "antlr4-runtime", version: "4.3"
	provided group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
}

generateGrammarSource {
	File grammarFile = file("src/main/antlr/${parserDirName}/DDMExpression.g4")

	deleteAllActions()

	dependsOn cleanGenerateGrammarSource

	doFirst {
		javaexec {
			args "-o", FileUtil.relativize(generateGrammarSource.outputDirectory, grammarFile.parentFile)
			args grammarFile.name
			args "-visitor"

			classpath = generateGrammarSource.antlrClasspath
			main = "org.antlr.v4.Tool"
			maxHeapSize = generateGrammarSource.maxHeapSize
			workingDir = grammarFile.parentFile
		}
	}

	doLast {
		copy {
			filter(FixCrLfFilter, javafiles: true, tab: FixCrLfFilter.AddAsisRemove.newInstance("add"))

			from outputDirectory
			include "**/*.java"
			into "src/main/java/" + parserDirName
		}

		copy {
			from outputDirectory
			exclude "**/*.java"
			into "src/main/resources/" + parserDirName
		}
	}

	ext {
		autoClean = false
	}

	maxHeapSize = "1512m"
	outputDirectory = new File(temporaryDir, "generated")
}