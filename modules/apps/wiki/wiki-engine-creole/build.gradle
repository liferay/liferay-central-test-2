import com.liferay.gradle.util.FileUtil

configurations {
	antlr

	provided {
		extendsFrom antlr
	}
}

dependencies {
	antlr group: "org.antlr", name: "antlr", version: "3.0.1"

	compile project(":apps:wiki:wiki-api")
	compile project(":apps:wiki:wiki-engine-input-editor-common")
}

task buildCreoleParser(type: JavaExec)

buildCreoleParser {
	File destinationDir = file("src/com/liferay/wiki/engine/creole/parser/parser")
	File grammarFile = file("src/com/liferay/wiki/engine/creole/parser/grammar/Creole10.g")

	args "-o", FileUtil.relativize(destinationDir, grammarFile.parentFile)
	args grammarFile.name

	classpath = configurations.antlr

	doFirst {
		delete destinationDir
	}

	doLast {
		ant.replace(file: "src/com/liferay/wiki/engine/creole/parser/parser/Creole10Lexer.java") {
			replacetoken "public class Creole10Lexer"

			replacevalue """\
@SuppressWarnings(\"all\")
public class Creole10Lexer"""
		}

		ant.replace(file: "src/com/liferay/wiki/engine/creole/parser/parser/Creole10Parser.java") {
			replacetoken "public class Creole10Parser"

			replacevalue """\
@SuppressWarnings(\"all\")
public class Creole10Parser"""
		}

		ant.fixcrlf includes: "*.java", javafiles: "yes", srcdir: "src/com/liferay/wiki/engine/creole/parser/parser", tab: "add"
	}

	main = "org.antlr.Tool"
	maxHeapSize = "1512m"
	workingDir = grammarFile.parentFile
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}