apply plugin: "antlr"

compileJava {
	dependsOn.remove "generateGrammarSource"
}

dependencies {
	antlr group: "org.antlr", name: "antlr4", version: "4.3"

	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
	compile project(":apps:dynamic-data-mapping:dynamic-data-mapping-api")
}

generateGrammarSource {
	File internalDir = file("src/com/liferay/dynamic/data/mapping/form/values/query/internal")
	File parserDir = new File(internalDir, "parser")

	doLast {
		File grammarDir = new File(internalDir, "grammar")

		ant.replace dir: parserDir, includes: "*.java", token: "${grammarDir}${File.separator}"

		ant.fixcrlf includes: "*.java", javafiles: "yes", srcdir: parserDir, tab: "add"

		File sourceFormatterIgnoreFile = new File(parserDir, "source_formatter.ignore")

		sourceFormatterIgnoreFile.createNewFile()
	}

	ext {
		autoClean = false
	}

	maxHeapSize = "1024m"
	outputDirectory = parserDir
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}

sourceSets {
	main {
		antlr {
			srcDirs = files("src/com/liferay/dynamic/data/mapping/form/values/query/internal/grammar")
		}
	}
}