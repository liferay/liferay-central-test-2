import com.liferay.gradle.util.copy.StripPathSegmentsAction

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

configurations {
	picturefill
}

task buildPictureFill(type: Copy)

String pictureFillVersion = "3.0.2"

buildPictureFill {
	eachFile new StripPathSegmentsAction(6)

	from {
		zipTree(configurations.picturefill.singleFile)
	}

	include "META-INF/resources/webjars/picturefill/${pictureFillVersion}/dist/picturefill.min.js"
	includeEmptyDirs = false
	into "classes/META-INF/resources"
}

classes {
	dependsOn buildPictureFill
}

dependencies {
	picturefill group: "org.webjars.bower", name: "picturefill", transitive: false, version: pictureFillVersion

	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	provided group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	provided project(":apps:adaptive-media:adaptive-media-api")
	provided project(":apps:adaptive-media:adaptive-media-image-impl")
	provided project(":apps:adaptive-media:adaptive-media-web")

	testCompile group: "junit", name: "junit", version: "4.12"
	testCompile group: "org.mockito", name: "mockito-core", version: "1.10.8", transitive: true
}