import org.kt3k.gradle.plugin.CoverallsPlugin

import org.gradle.testing.jacoco.tasks.JacocoReport

buildscript {
	dependencies {
		classpath group: "org.kt3k.gradle.plugin", name: "coveralls-gradle-plugin", version: "2.6.3"
	}

	repositories {
		mavenLocal()

		maven {
			url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
		}
	}
}

apply plugin: CoverallsPlugin

task checkDocker
task copyJacocoAgent(type: Copy)
task coverageReport(type: JacocoReport)
task createTempDir(type: Exec)
task loginDocker(type: Exec)
task rmDocker(type: Exec)
task runDocker(type: Exec)
task startJacocoRecording
task stopDocker(type: Exec)
task stopJacocoRecording(type: Exec)

Iterable<Project> moduleProjects = subprojects.findAll{
	!it.childProjects
}

FileCollection moduleSrcDirs = files(moduleProjects.sourceSets.main.allSource.srcDirs)

allprojects {
	apply plugin: "jacoco"

	jacoco {
		toolVersion = "0.7.8"
	}
}

checkDocker {
	doLast {
		URL url = new URL("http://localhost:8080")

		println "Trying to reach ${url}"

		while (true) {
			try {
				print "."

				url.text

				println "Portal startup finished"

				break
			}
			catch (Exception e) {
				Thread.sleep 1000
			}
		}
	}
}

copyJacocoAgent {
	configurations.jacocoAgent.asFileTree.each {
		from(zipTree(it))
	}

	into "${rootDir}/build/jacoco"
}

coveralls {
	jacocoReportPath = new File(buildDir, "reports/jacoco/coverageReport/coverageReport.xml")

	sourceDirs = moduleSrcDirs.collect {
		file -> file.absolutePath
	}
}

coverageReport {
	additionalSourceDirs = moduleSrcDirs

	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
			fileTree(
				dir: it,
				exclude: [
					"**/*Exception*",
					"**/*ServiceUtil*",
					"**/*ServiceWrapper*",
					"**/constants/*",
					"**/exception/*",
					"**/model/*",
					"**/model/impl/*",
					"**/persistence/*",
					"**/service/base/*",
					"**/service/util/*"
				])
		})
	}

	classDirectories = files(moduleProjects.sourceSets.main.output)

	doFirst {
		executionData = executionData.filter {
			it.exists()
		}
	}

	executionData = files(moduleProjects.jacocoTestReport.executionData, new File(buildDir, "jacoco/testIntegration.exec"))

	onlyIf = {
		true
	}

	reports {
		html.enabled = true
		xml.enabled = true
	}

	sourceDirectories = moduleSrcDirs
}

createTempDir {
	commandLine "mkdir", "-m", "777", "-p", "${rootDir}/build/tmpdir"
}

loginDocker {
	args "login"

	doFirst {
		if (_isAlreadyLoggedIn()) {
			println "User already logged in in Docker"

			throw new StopExecutionException()
		}

		println "User not logged in into Docker yet"

		if (!dockerPwd || !dockerUser) {
			println "DOCKER_USER or DOCKER_PWD environment variables not set"

			def console = System.console()

			if (console == null) {
				throw new GradleException("It is not possible to read Docker credentials.")
			}

			println "Using system.in to prompt for Docker user and password"

			println "Please enter docker user:"

			dockerUser = console.readLine()

			if (!dockerUser) {
				throw new GradleException("You must enter Docker user or set DOCKER_USER environment variable to proceed.")
			}

			println "Please enter Docker password:"

			dockerPwd = console.readPassword()

			if (!dockerPwd) {
				throw new GradleException("You must enter Docker password or set DOCKER_PWD environment variable to proceed.")
			}

			args "--username=${dockerUser}"
			args "--password=${dockerPwd}"
		}
	}

	executable = "docker"

	ext {
		dockerPwd = System.getenv("DOCKER_PWD")
		dockerUser = System.getenv("DOCKER_USER")
	}
}

rmDocker {
	commandLine "docker", "rm", "com-liferay-adaptive-media"
	dependsOn stopDocker
	ignoreExitValue = true
}

runDocker {
	String javaOpts = project.getProperty("com.liferay.adaptive.media.java.opts")

	javaOpts += " " + project.getProperty("com.liferay.adaptive.media.jacoco.opts")

	if (project.hasProperty("debug")) {
		javaOpts += " " + project.getProperty("com.liferay.adaptive.media.debug.opts")
	}

	args "run"
	args "-h", "localhost"
	args "-e", "JAVA_OPTS=$javaOpts"
	args "-p", "5005:5005"
	args "-p", "8080:8080"
	args "-p", "8099:8099"
	args "-p", "11311:11311"
	args "-v", "${rootDir}/../bundles/deploy:/liferay/deploy"
	args "-v", "${rootDir}/docker/portal-ext.properties:/liferay/portal-ext.properties"
	args "-v", "${rootDir}/build/tmpdir:${rootDir}/build/tmpdir"
	args "-v", "${rootDir}/build/jacoco:/jacoco"
	args "-d"
	args "--name", "com-liferay-adaptive-media"
	args "liferay/liferay-de:20170421073001106222642-db"

	dependsOn createTempDir
	dependsOn loginDocker
	dependsOn rmDocker
	dependsOn copyJacocoAgent

	executable = "docker"
	finalizedBy checkDocker
}

startJacocoRecording {
	doLast {
		File file = new File(buildDir, "jacoco/testIntegration.exec")

		if (file.exists()) {
			file.deleteOnExit()
		}

		file.parentFile.mkdirs()

		file.createNewFile()
	}
}

stopDocker {
	commandLine "docker", "stop", "com-liferay-adaptive-media"
	ignoreExitValue = true
}

stopJacocoRecording {
	commandLine "docker", "exec", "-d", "com-liferay-adaptive-media", "env", "JAVA_OPTS=", "/liferay/tomcat-8.0.32/bin/shutdown.sh"
}

configure(subprojects.findAll {!it.childProjects}) {
	task deployRuntimeLibs(type: Copy)

	configurations {
		runtimeLib {
			description = "Configures deployable runtime libs."
		}

		compile.extendsFrom runtimeLib
	}

	deployRuntimeLibs {
		from configurations.runtimeLib

		into {
			liferay.deployDir
		}

		rename {
			String fileName ->

			fileName.replaceAll "(.*)-((\\d\\.{1})*\\d)\\.jar", "\$1.jar"
		}
	}

	jacocoTestReport {
		additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
		classDirectories =  files(sourceSets.main.output)

		reports {
			html.enabled = true
			xml.enabled = true
		}

		sourceDirectories = files(sourceSets.main.allSource.srcDirs)
	}

	liferay {
		deployDir = new File(rootDir, "../bundles/deploy")
	}

	startTestableTomcat {
		enabled = false
	}

	stopTestableTomcat {
		enabled = false
	}

	test {
		ignoreFailures = false
	}

	testIntegration {
		dependsOn createTempDir
		ignoreFailures = false
		systemProperty "java.io.tmpdir", new File(buildDir, "tmpdir")
	}

	if (project.hasProperty("docker")) {
		setUpTestableTomcat {
			enabled = false
		}
	}

	rootProject.runDocker {
		dependsOn deploy
		dependsOn deployRuntimeLibs
	}
}

repositories {
	mavenLocal()

	maven {
		url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
	}
}

tasks.coveralls {
	dependsOn coverageReport
}

private boolean _isAlreadyLoggedIn() {
	File dockerConfigFile = new File(System.getenv("HOME") + "/.docker/config.json")

	if (dockerConfigFile.exists() && dockerConfigFile.text.contains("https://index.docker.io/v1/")) {
		return true
	}

	return false
}