import com.liferay.gradle.plugins.gulp.ExecuteGulpTask
import com.liferay.gradle.plugins.util.GradleUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

task expandFrontendCSSCommon(type: Copy)

File frontendCSSCommonDir = new File(buildDir, "frontend-css-common")
String frontendCSSCommonVersion = GradleUtil.getPortalToolVersion(project, "com.liferay.frontend.css.common")

configurations {
	frontendCSSCommon
}

dependencies {
	frontendCSSCommon group: "com.liferay", name: "com.liferay.frontend.css.common", version: frontendCSSCommonVersion
}

expandFrontendCSSCommon {
	doFirst {
		delete frontendCSSCommonDir
	}

	eachFile new StripPathSegmentsAction(2)

	from {
		zipTree(configurations.frontendCSSCommon.singleFile)
	}

	include "META-INF/resources/"
	into frontendCSSCommonDir
	includeEmptyDirs = false
}

repositories {
	mavenLocal()

	maven {
		url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
	}
}

tasks.withType(ExecuteGulpTask) {
	Project styledProject = parent.project("frontend-theme-styled")
	Project unstyledProject = parent.project("frontend-theme-unstyled")

	File styledDir = new File(styledProject.projectDir, "src/main/resources/META-INF/resources/_styled")
	File unstyledDir = new File(unstyledProject.projectDir, "src/main/resources/META-INF/resources/_unstyled")

	args "--styled-path=" + styledDir.absolutePath
	args "--unstyled-path=" + unstyledDir.absolutePath
	args "--css-common-path=" + frontendCSSCommonDir.absolutePath

	dependsOn expandFrontendCSSCommon
	dependsOn "${unstyledProject.path}:buildTheme"
}