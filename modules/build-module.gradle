import com.liferay.gradle.util.FileUtil

import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
	repositories {
		if (!Boolean.getBoolean("maven.local.ignore")) {
			mavenLocal()
		}

		maven {
			url System.properties["repository.url"] ?: "http://cdn.repository.liferay.com/nexus/content/groups/public"
		}
	}

	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.19"
	}
}

apply plugin: "com.liferay.plugin"
apply plugin: "eclipse"
apply plugin: "findbugs"

File appServerClassesPortalDir = new File(liferay.appServerPortalDir, "WEB-INF/classes")
File appServerLibPortalDir = new File(liferay.appServerPortalDir, "WEB-INF/lib")

distsDirName = FileUtil.relativize(rootProject.file("../tools/sdk/dist"), buildDir)
group = "com.liferay"
libsDirName = distsDirName
sourceCompatibility = "1.7"
targetCompatibility = "1.7"

afterEvaluate {
	project.ext.set("app.server.lib.portal.dir", project.relativePath(appServerLibPortalDir))
	project.ext.set("app.server.portal.dir", project.relativePath(liferay.appServerPortalDir))
	project.ext.set("plugin.full.version", project.version.toString())

	if (project.hasProperty("snapshot") && !project.version.endsWith("-SNAPSHOT")) {
		project.version = project.version + "-SNAPSHOT"
	}

	Iterable<Task> publishNodeModuleTasks = tasks.matching {
		it.class.name.startsWith "com.liferay.gradle.plugins.node.tasks.PublishNodeModuleTask"
	}

	publishNodeModuleTasks.each {
		it.dependsOn jar

		if (!project.version.endsWith("-SNAPSHOT")) {
			uploadArchives.dependsOn it
		}
	}

	javadoc {
		classpath = sourceSets.main.output + sourceSets.main.compileClasspath
	}
}

buildCSS {
	dependsOn ":frontend:frontend-common-css:buildCssCommon"
	portalCommonDirName = relativePath(new File(rootDir, "frontend/frontend-common-css/tmp/META-INF/resources"))
}

buildService {
	doFirst {
		if (project.name.endsWith("-service")) {
			String requiredApiDirName = "../${project.name[0 .. -9]}-api/src"

			if (apiDirName != requiredApiDirName) {
				throw new GradleException("Please configure the 'buildService' task.")
			}
		}
	}
}

buildSoy {
	ext {
		autoClean = false
	}
}

configure([test, testIntegration]) {
	testLogging {
		events = EnumSet.allOf(TestLogEvent)
		exceptionFormat = "full"
		stackTraceFilters = []
	}
}

configurations {
	all {
		resolutionStrategy {
			eachDependency {
				DependencyResolveDetails dependencyResolveDetails ->

				if ((dependencyResolveDetails.requested.version == "default") && project.hasProperty(dependencyResolveDetails.requested.name + ".version")) {
					dependencyResolveDetails.useVersion project.properties[dependencyResolveDetails.requested.name + ".version"]
				}
			}
		}
	}

	portal
	portalEclipse
	portalTest

	alloyTaglib {
		extendsFrom configurations.runtime
		extendsFrom portal
	}
}

if (FileUtil.exists(project, "docroot")) {
	copyLibs {
		from(appServerLibPortalDir) {
			include "util-bridges.jar", "util-java.jar", "util-taglib.jar"
		}
	}
}

dependencies {
	alloyTaglib rootProject.files("../lib/development/alloy-taglib.jar", "../lib/development/org.freemarker-2.3.17.LIFERAY-PATCHED-1.jar")

	cssBuilder group: "com.liferay", name: "com.liferay.css.builder", version: "default"
	javadocFormatter group: "com.liferay", name: "com.liferay.javadoc.formatter", version: "default"
	langBuilder group: "com.liferay", name: "com.liferay.lang.builder", version: "default"

	portal files(appServerClassesPortalDir)
	portal fileTree(dir: appServerLibPortalDir, include: "*.jar")
	portal fileTree(dir: liferay.appServerLibGlobalDir, exclude: "mail.jar", include: "*.jar")
	portal group: "com.liferay", name: "net.sf.jargs", version: "1.0"
	portal group: "com.thoughtworks.qdox", name: "qdox", version: "1.12.1"
	portal group: "javax.activation", name: "activation", version: "1.1"
	portal group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	portal group: "javax.servlet.jsp", name: "jsp-api", version: "2.1"

	portalEclipse group: "biz.aQute.bnd", name: "biz.aQute.bnd", version: "2.4.1"
	portalEclipse group: "commons-logging", name: "commons-logging", version: "1.1.3"
	portalEclipse group: "javax.activation", name: "activation", version: "1.1"
	portalEclipse group: "javax.annotation", name: "jsr250-api", version: "1.0"
	portalEclipse group: "javax.mail", name: "mail", version: "1.4"
	portalEclipse group: "javax.portlet", name: "portlet-api", version: "2.0"
	portalEclipse group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	portalEclipse group: "javax.servlet.jsp", name: "jsp-api", version: "2.1"
	portalEclipse group: "log4j", name: "log4j", version: "1.2.17"

	portalTest group: "com.liferay.portal", name: "portal-test", version: liferay.portalVersion
	portalTest group: "com.liferay.portal", name: "portal-test-internal", version: liferay.portalVersion

	serviceBuilder fileTree(dir: rootProject.file("../tools/sdk/tmp/portal-tools"), include: "*.jar")
	sourceFormatter group: "com.liferay", name: "com.liferay.source.formatter", version: "default"
	tldFormatter group: "com.liferay", name: "com.liferay.tld.formatter", version: "default"
	upgradeTableBuilder group: "com.liferay", name: "com.liferay.portal.tools.upgrade.table.builder", version: "default"
	wsddBuilder group: "com.liferay", name: "com.liferay.portal.tools.wsdd.builder", version: "dependencies"
	xmlFormatter group: "com.liferay", name: "com.liferay.xml.formatter", version: "default"
}

eclipse {
	classpath {
		file {
			whenMerged {
				it.entries.removeAll {
					(it.kind == "lib") && it.path.endsWith(".pom")
				}
			}

			withXml {
				Node node = it.asNode()

				node.appendNode "classpathentry", [kind: "src", path: "/portal-master"]

				["portal-service", "util-bridges", "util-java", "util-taglib"].each {
					node.appendNode "classpathentry", [kind: "lib", path: "/portal-master/${it}/${it}.jar"]
				}
			}
		}

		plusConfigurations += [configurations.portalEclipse]
	}
}

eclipseClasspath {
	ext {
		autoClean = false
	}
}

eclipseProject {
	ext {
		autoClean = false
	}
}

jsModuleConfigGenerator {
	version = project.properties["nodejs.lfr.module.config.generator.version"]
}

jspC {
	jspCVersion = project.properties["com.liferay.jasper.jspc.version"]
}

jsTranspiler {
	babelVersion = project.properties["nodejs.babel.version"]
	lfrAmdLoaderVersion = project.properties["nodejs.lfr.amd.loader.version"]
}

node {
	npmArgs "--fetch-retries", "10"
	npmArgs "--fetch-retry-maxtimeout", "120000"
	npmArgs "--fetch-retry-mintimeout", "60000"
}

repositories {
	if (!Boolean.getBoolean("maven.local.ignore")) {
		mavenLocal()
	}

	maven {
		url System.properties["repository.url"] ?: "http://cdn.repository.liferay.com/nexus/content/groups/public"
	}
}

sourceSets {
	main {
		compileClasspath += configurations.portal
	}

	test {
		compileClasspath = configurations.compile + configurations.portal + compileClasspath + configurations.portalTest
		runtimeClasspath = configurations.compile + configurations.portal + runtimeClasspath + configurations.portalTest
	}

	testIntegration {
		compileClasspath = configurations.portal + compileClasspath + configurations.portalTest
		runtimeClasspath = configurations.portal + runtimeClasspath + configurations.portalTest
	}
}

tasks.eclipse {
	dependsOn cleanEclipse
}

whip {
	version = project.properties["com.liferay.whip.version"]
}

pluginManager.withPlugin("com.liferay.maven.plugin.builder") {
	afterEvaluate {
		buildPluginDescriptor {
			pomArtifactId = bundle.instructions["Bundle-SymbolicName"]
		}
	}

	buildPluginDescriptor {
		configurationScopeMapping "provided", "provided"

		ext {
			autoClean = false
		}
	}
}

tasks.withType(FindBugs) {
	maxHeapSize = "1g"

	reports {
		html.enabled = true
		xml.enabled = false
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	options.warnings = false
}

if (gradle.hasProperty("testClassGroupIndex")) {
	configure([test, testIntegration]) {
		include gradle.testClasses

		jvmArgs "-Dtest.class.group.index=" + gradle.testClassGroupIndex
		jvmArgs "-Dtest.class.groups=" + gradle.testClassGroups
	}
}

if (System.getenv("JENKINS_HOME")) {
	node {
		nodeUrl = "https://nodejs.org/dist/v${nodeVersion}/node-v${nodeVersion}-linux-x64.tar.gz"
	}

	pluginManager.withPlugin("com.liferay.cache") {
		cache {
			forcedCache = true
		}
	}

	startTestableTomcat {
		ext {
			deleteLiferayHome = false
		}
	}
}
