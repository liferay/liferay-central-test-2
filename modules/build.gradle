import com.liferay.gradle.util.FileUtil

import org.gradle.internal.hash.HashUtil
import org.gradle.internal.hash.HashValue

buildscript {
	apply from: rootProject.file("build-buildscript.gradle"), to: buildscript
}

buildDir = new File(rootDir.parentFile, "build")

gradle.beforeProject {
	Project project ->

	if (!project.parent) {
		return
	}

	configure(project) {
		if (project.path.startsWith(":apps:foundation:frontend-theme:") && FileUtil.exists(project, "gulpfile.js")) {
			apply from: rootProject.file("build-theme.gradle")
		}
		else if (FileUtil.exists(project, "bnd.bnd") && FileUtil.exists(project, "build.gradle")) {
			apply from: rootProject.file("build-module.gradle")
			apply plugin: "pmd"

			task installCache(type: InstallCacheTask)

			configurations {
				xmltask
			}

			dependencies {
				pmd group: "com.liferay", name: "com.liferay.pmd", version: "1.0.1"
				xmltask rootProject.files("../lib/development/xmltask.jar")
			}

			installCache {
				artifactGroup = "${->project.group}"
				artifactName = "${->project.archivesBaseName}"
				artifactVersion = "${->project.version}"

				dependsOn cleanInstallCache
				dependsOn install

				description = "Installs the project to the local Gradle cache for testing."

				ext {
					autoClean = false
				}

				group = "upload"
			}

			pmd {
				ruleSetFiles = rootProject.files("../tools/sdk/dependencies/net.sourceforge.pmd/rulesets/java/standard-rules.xml")
				ruleSets = []
			}

			testIntegration {
				doLast {
					String databaseType = System.properties["database.type"]

					if (!databaseType) {
						return
					}

					FileTree fileTree = fileTree(dir: reports.junitXml.destination, exclude: "TEST-*-*.xml", include: "TEST-*.xml")

					ant.taskdef classname: "com.oopsconsultancy.xmltask.ant.XmlTask", classpath: configurations.xmltask.asPath, name: "xmltask"

					fileTree.each {
						File file ->

						String fileTitle = FileUtil.stripExtension(file.name)

						ant.xmltask(dest: new File(file.parentFile, "${fileTitle}-${databaseType}.xml"), source: file) {
							replace path: "/testsuite/@name", withText: "${fileTitle}-${databaseType}"
						}

						delete file
					}
				}
			}

			tasks.withType(Pmd) {
				classpath = null
			}

			if (gradle.hasProperty("testClassGroupIndex")) {
				configure([test, testIntegration]) {
					include gradle.testClasses

					jvmArgs "-Dtest.class.group.index=" + gradle.testClassGroupIndex
					jvmArgs "-Dtest.class.groups=" + gradle.testClassGroups
				}
			}

			if (System.getenv("JENKINS_HOME")) {
				buildCSS {
					jvmArgs "-Xss4m"
				}

				startTestableTomcat {
					deleteLiferayHome = false
				}
			}
		}

		if (System.getenv("JENKINS_HOME")) {
			pluginManager.withPlugin("com.liferay.node") {
				downloadNode {
					doFirst {
						throw new GradleException("Unable to use Node.js on CI, please configure com.liferay.cache or update the cache")
					}
				}
			}
		}
	}
}

gradle.taskGraph.useFilter {
	Task task ->

	if (task.name == "compileJSP") {
		FileTree jspFileTree = fileTree(dir: task.project.projectDir, excludes: ["**/custom_jsps/**/*", "**/dependencies/**/*"], includes: ["docroot/**/*.jsp", "src/main/resources/META-INF/resources/**/*.jsp"])

		if (jspFileTree.empty) {
			return false
		}
	}
	else if (task.name == "test") {
		if (!_hasTestClasses(task.project, "src/test/java")) {
			return false
		}
	}
	else if (task.name == "testIntegration") {
		if (!_hasTestClasses(task.project, "src/testIntegration/java")) {
			return false
		}
	}

	return true
}

private boolean _hasTestClasses(Project project, String testClassesDirName) {
	File testClassesDir = project.file(testClassesDirName)

	if (!testClassesDir.exists()) {
		return false
	}

	if (project.gradle.hasProperty("testClassGroupIndex")) {
		List<String> testJavaClasses = project.gradle.testClasses.collect {
			it.replace ".class", ".java"
		}

		FileTree testJavaClassesFileTree = project.fileTree(dir: testClassesDir, includes: testJavaClasses)

		if (testJavaClassesFileTree.empty) {
			return false
		}
	}

	return true
}

class InstallCacheTask extends DefaultTask {

	@Input
	Object artifactGroup

	@Input
	Object artifactName

	@Input
	Object artifactVersion

	@Input
	File cacheRootDir = new File(project.gradle.gradleUserHomeDir, "caches/modules-2/files-2.1")

	@Input
	File mavenRootDir = new File(System.properties["user.home"], ".m2/repository")

	@OutputDirectory
	File getCacheDestinationDir() {
		new File(cacheRootDir, "${artifactGroup}/${artifactName}/${artifactVersion}")
	}

	@InputDirectory
	File getMavenInputDir() {
		String mavenArtifactGroup = artifactGroup.replace('.' as char, File.separatorChar)

		new File(mavenRootDir, "${mavenArtifactGroup}/${artifactName}/${artifactVersion}")
	}

	@TaskAction
	void installCacheTask() {
		["jar", "pom"].each {
			String extension ->

			File file = new File(mavenInputDir, "${artifactName}-${artifactVersion}.${extension}")

			if (!file.exists()) {
				throw new GradleException("Unable to find " + file)
			}

			if (extension == "pom") {
				File tempFile = new File(temporaryDir, file.name)

				project.copy {
					from file
					into temporaryDir
				}

				file = tempFile

				CharsetToolkit charsetToolkit = new CharsetToolkit(file)

				String charset = charsetToolkit.charset.name()

				ant.fixcrlf encoding: charset, eol: "lf", file: file, fixlast: false, outputencoding: "UTF-8"
			}

			HashValue hashValue = HashUtil.sha1(file)

			String hash = hashValue.asHexString()

			hash.replaceFirst(/^0*/, "")

			project.copy {
				from file
				into new File(cacheDestinationDir, hash)
			}
		}
	}

}