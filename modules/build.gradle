buildscript {
	repositories {
		mavenLocal()

		maven {
			url "http://cdn.repository.liferay.com/nexus/content/groups/public"
		}
	}

	dependencies {
		classpath group: "com.liferay", name: "com.liferay.ant.bnd", version: "latest.release"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins", version: "+"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.maven.plugin.builder", version: "latest.release"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.patcher", version: "latest.release"
	}
}

gradle.beforeProject {
	Project project ->

	File buildGradleFile = project.file("build.gradle")

	if (buildGradleFile.exists() && project.parent) {
		project.apply from: rootProject.file("build-module.gradle")
	}
}

task initMissingGradle

initMissingGradle {
	dependsOn {
		FileTree buildGradleFileTree = fileTree(dir: rootDir, include: "*/**/build.gradle")

		URI rootURI = rootDir.toURI()

		buildGradleFileTree.findResults {
			File buildGradleFile ->

			if (buildGradleFile.length() > 0) {
				return null
			}

			String moduleDirName = rootURI.relativize(buildGradleFile.toURI())

			moduleDirName = moduleDirName.replaceFirst("/build.gradle", "")

			return ":" + moduleDirName.replaceAll("[\\/]", ":") + ":initGradle"
		}
	}
}