import com.liferay.gradle.plugins.node.tasks.PublishNodeModuleTask
import com.liferay.gradle.plugins.tasks.BuildThumbnailsTask
import com.liferay.gradle.plugins.tasks.CompileThemeTask
import com.liferay.gradle.util.GradleUtil

configurations {
	frontendThemes
}

task buildTheme(type: CompileThemeTask)
task buildThumbnails(type: BuildThumbnailsTask)
task copyFavicon(type: Copy)
task publishNodeModule(type: PublishNodeModuleTask)

File themeDestinationDir = file("src/main/resources/META-INF/resources/classic")

buildCSS {
	dependsOn buildTheme
}

buildTheme {
	dependsOn buildThumbnails
	dependsOn copyFavicon

	diffsDir = new File(themeDestinationDir, "_diffs")
	frontendThemeFiles = configurations.frontendThemes
	themeParent = "_styled"
	themeRootDir = themeDestinationDir
	themeTypes = ["ftl"]
}

buildThumbnails {
	imageDirs themeDestinationDir
}

classes {
	dependsOn buildTheme
}

copyFavicon {
	ext {
		autoClean = false
	}

	from "../frontend-theme-unstyled/src/main/resources/META-INF/resources/_unstyled/images/favicon.ico"
	into "src/main/resources/META-INF/resources"
}

dependencies {
	provided group: "com.liferay.portal", name: "portal-service", version: liferay.portalVersion
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	provided group: "org.osgi", name: "org.osgi.core", version: "5.0.0"

	frontendThemes project(":frontend:frontend-theme:frontend-theme-styled")
	frontendThemes project(":frontend:frontend-theme:frontend-theme-unstyled")
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}

publishNodeModule {
	dependsOn jar
	workingDir = buildTheme.diffsDir
}