import com.liferay.gradle.plugins.node.tasks.PublishNodeModuleTask
import com.liferay.gradle.util.copy.StripPathSegmentsAction

configurations {
	alloyUI
	lexicon
}

task buildAlloyUITheme(type: Copy)
task buildLexicon(type: Copy)
task buildLexiconFonts(type: Copy)
task buildLexiconIcons(type: Copy)
task buildTheme
task publishNodeModule(type: PublishNodeModuleTask)

String alloyUIVersion = "3.0.3-deprecated.5"
String lexiconVersion = "0.1.28"

File themeDestinationDir = file("src/main/resources/META-INF/resources/_unstyled");

buildAlloyUITheme {
	File alloyThemeDestinationDir = new File(themeDestinationDir, "images/aui")

	doFirst {
		delete alloyThemeDestinationDir
	}

	eachFile new StripPathSegmentsAction(6)

	filesMatching "build/aui-*/assets/skins/sam/", new StripPathSegmentsAction(5)
	filesMatching "build/aui-skin-deprecated/images/", new StripPathSegmentsAction(3)

	from {
		zipTree(configurations.alloyUI.singleFile)
	}

	include "META-INF/resources/webjars/alloy-ui/${alloyUIVersion}/build/aui-*/assets/skins/sam/**.gif"
	include "META-INF/resources/webjars/alloy-ui/${alloyUIVersion}/build/aui-*/assets/skins/sam/**.jpg"
	include "META-INF/resources/webjars/alloy-ui/${alloyUIVersion}/build/aui-*/assets/skins/sam/**.png"
	include "META-INF/resources/webjars/alloy-ui/${alloyUIVersion}/build/aui-skin-deprecated/images/"

	includeEmptyDirs = false
	into alloyThemeDestinationDir
}

buildCSS {
	dependsOn buildTheme
}

buildLexicon {
	File lexiconDestinationDir = new File(themeDestinationDir, "css/aui/lexicon")

	doFirst {
		delete lexiconDestinationDir
	}

	eachFile {
		if (name in ["atlas.scss", "bootstrap.scss", "lexicon-base.scss"]) {
			filter {
				String line ->

				if (line == "// INSERT CUSTOM EXTENSIONS") {
					line = """\
@import \"../../liferay_variables_custom\";
@import \"../../liferay_variables\";
@import \"../../aui_custom\";
@import \"../../liferay_custom\";"""
				}
				else if (line == "// INSERT CUSTOM VARS") {
					line = """\
@import \"../../aui_variables\";
@import \"fonts/alloy-font-awesome/scss/font-awesome\";"""
				}

				return line
			}
		}
		else if (name in ["atlas-variables.scss", "lexicon-base-variables.scss"]) {
			filter {
				String line ->

				if (line == "// INSERT CUSTOM BASE VARS") {
					line = """\
@import \"../../aui_variables\";
@import \"fonts/alloy-font-awesome/scss/variables\";"""
				}
				else if (line == "// INSERT CUSTOM VARS") {
					line = """\
@import \"../../liferay_variables_custom\";
@import \"../../liferay_variables\";"""
				}

				return line
			}
		}
	}

	eachFile new StripPathSegmentsAction(7)

	from {
		zipTree(configurations.lexicon.singleFile)
	}

	include "META-INF/resources/webjars/lexicon/${lexiconVersion}/release/scss/"
	includeEmptyDirs = false
	into lexiconDestinationDir
}

buildLexiconFonts {
	File lexiconFontsDestinationDir = new File(themeDestinationDir, "css/aui/lexicon/fonts")

	dependsOn buildLexicon

	doFirst {
		delete lexiconFontsDestinationDir
	}

	eachFile new StripPathSegmentsAction(7)

	from {
		zipTree(configurations.lexicon.singleFile)
	}

	include "META-INF/resources/webjars/lexicon/${lexiconVersion}/release/fonts/"
	includeEmptyDirs = false
	into lexiconFontsDestinationDir
}

buildLexiconIcons {
	File lexiconIconsDestinationDir = new File(themeDestinationDir, "images/lexicon")

	doFirst {
		delete lexiconIconsDestinationDir
	}

	eachFile new StripPathSegmentsAction(8)

	from {
		zipTree(configurations.lexicon.singleFile)
	}

	include "META-INF/resources/webjars/lexicon/${lexiconVersion}/release/images/icons/"
	includeEmptyDirs = false
	into lexiconIconsDestinationDir
}

buildTheme {
	dependsOn buildAlloyUITheme
	dependsOn buildLexiconFonts
	dependsOn buildLexiconIcons
}

classes {
	dependsOn buildTheme
}

dependencies {
	alloyUI group: "org.webjars.bower", name: "alloy-ui", transitive: false, version: alloyUIVersion

	compile group: "com.liferay.portal", name: "portal-service", version: liferay.portalVersion
	compile group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"

	lexicon group: "org.webjars.bower", name: "lexicon", transitive: false, version: lexiconVersion
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}

publishNodeModule {
	dependsOn jar
	workingDir = "src/main/resources/META-INF/resources/_unstyled"
}