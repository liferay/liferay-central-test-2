import com.liferay.gradle.plugins.tasks.BuildThumbnailsTask
import com.liferay.gradle.plugins.tasks.CompileThemeTask
import com.liferay.gradle.util.GradleUtil

configurations {
	frontendThemes
}

dependencies {
	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"

	frontendThemes project(":frontend:frontend-themes:frontend-themes-styled")
	frontendThemes project(":frontend:frontend-themes:frontend-themes-unstyled")
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}

File themeDestinationDir = file("src/META-INF/resources/classic")

task buildTheme(type: CompileThemeTask)
task buildThumbnails(type: BuildThumbnailsTask)
task copyFavicon(type: Copy)

buildCSS {
	dependsOn buildTheme
}

buildTheme {
	dependsOn buildThumbnails
	dependsOn copyFavicon

	diffsDir = new File(themeDestinationDir, "_diffs")
	frontendThemeFiles = configurations.frontendThemes
	themeParent = "_styled"
	themeRootDir = themeDestinationDir
	themeTypes = ["ftl", "vm"]
}

buildThumbnails {
	imageDirs themeDestinationDir
}

classes {
	dependsOn buildTheme
}

copyFavicon {
	ext {
		autoClean = false
	}

	from "../frontend-themes-unstyled/src/META-INF/resources/_unstyled/images/favicon.ico"
	into "src/META-INF/resources"
}