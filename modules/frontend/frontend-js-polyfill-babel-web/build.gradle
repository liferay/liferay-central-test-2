import com.liferay.gradle.plugins.node.tasks.DownloadNodeModuleTask

apply plugin: "com.liferay.cache"

task buildBabelCore(type: Copy)
task downloadBabelCore(type: DownloadNodeModuleTask)

String babelCoreVersion = "5.6.14"

buildBabelCore {
	dependsOn downloadBabelCore
	from new File(node.nodeDir, "node_modules/babel-core/browser-polyfill.min.js")
	into "classes/META-INF/resources"
}

cache {
	task(buildBabelCore) {
		baseDir = buildBabelCore.destinationDir
		cacheDir = ".task-cache"
		include "browser-polyfill.min.js"
		testFile "build.gradle"
	}
}

classes {
	dependsOn buildBabelCore
}

dependencies {
	compile group: "com.liferay.portal", name: "portal-service", version: liferay.portalVersion
	compile group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
}

downloadBabelCore {
	moduleName = "babel-core"
	moduleVersion = babelCoreVersion
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}