import com.liferay.gradle.plugins.node.tasks.DownloadNodeModuleTask
import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.ReplaceLeadingPathAction
import com.liferay.gradle.util.copy.StripPathSegmentsAction

apply plugin: "com.liferay.task-cache"

task buildAlloy(type: Copy)
task buildAlloyBootstrap(type: Copy)
task buildJQueryPluginsForm
task buildLexicon(type: Copy)
task buildLfrAMDLoader(type: Copy)
task downloadLfrAMDLoader(type: DownloadNodeModuleTask)

String alloyBootstrapVersion = "3.2.0-2"
String alloyVersion = "3.0.1"
String jqueryPluginsFormVersion = "3.51"
String lexiconVersion = "0.1.16"
String lfrAMDLoaderVersion = "1.3.5"

File jsDestinationDir = file("tmp/META-INF/resources")
File thirdPartyDir = file("../../../portal-web/third-party")

buildAlloy {
	File alloyDestinationDir = new File(jsDestinationDir, "aui")
	File alloyZipFile = new File(thirdPartyDir, "alloy-${alloyVersion}.zip")

	doFirst {
		delete alloyDestinationDir
	}

	doLast {
		File auiDeprecatedCssFile = new File(alloyDestinationDir, "aui_deprecated.css")

		File auiSkinDeprecatedCssFile = new File(alloyDestinationDir, "aui-skin-deprecated/css/aui-skin-deprecated.css")
		FileTree cssFileTree = fileTree(dir: alloyDestinationDir, include: "*-deprecated/assets/skins/sam/*-deprecated.css")

		FileUtil.concatenate(auiDeprecatedCssFile, files(auiSkinDeprecatedCssFile, cssFileTree))

		ant.replaceregexp(file: auiDeprecatedCssFile, flags: "g", match: "url\\(([^)]+)\\)", replace: "url(@theme_image_path@/aui/\\1)")

		ant.replace(file: auiDeprecatedCssFile) {
			replacefilter token: "../images/", value: ""
			replacefilter token: "../../../../../build/aui-skin-base/images/", value: ""
			replacefilter token: "../../../../../build/aui-skin-classic/images/", value: ""
			replacefilter token: "../../../../../build/aui-skin-deprecated/images/", value: ""
		}
	}

	eachFile new StripPathSegmentsAction(1)
	from zipTree(alloyZipFile)
	include "build/**"
	includeEmptyDirs = false
	into alloyDestinationDir

	onlyIf {
		!FileUtil.isUpToDate(project, alloyZipFile, alloyDestinationDir)
	}
}

buildAlloyBootstrap {
	File alloyBootstrapAssetsDestinationDir = new File(jsDestinationDir, "bootstrap-assets")
	File alloyBootstrapDestinationDir = new File(jsDestinationDir, "bootstrap")
	File bootstrapSassZipFile = new File(thirdPartyDir, "bootstrap-sass-${alloyBootstrapVersion}.zip")

	doFirst {
		delete alloyBootstrapAssetsDestinationDir, alloyBootstrapDestinationDir
	}

	eachFile new ReplaceLeadingPathAction("bootstrap-sass-${alloyBootstrapVersion}/assets/javascripts": alloyBootstrapDestinationDir.name, "bootstrap-sass-${alloyBootstrapVersion}/assets": alloyBootstrapAssetsDestinationDir.name)
	exclude "bootstrap-sass-${alloyBootstrapVersion}/assets/javascripts/bootstrap-*.js"
	from zipTree(bootstrapSassZipFile)
	include "bootstrap-sass-${alloyBootstrapVersion}/assets/**"
	includeEmptyDirs = false
	into jsDestinationDir

	onlyIf {
		!FileUtil.isUpToDate(project, bootstrapSassZipFile, alloyBootstrapDestinationDir)
	}
}

buildJQueryPluginsForm {
	File jQueryPluginsFormJsFile = file("src/META-INF/resources/jquery/form.js")

	doLast {
		FileUtil.get(project, "https://raw.githubusercontent.com/malsup/form/${jqueryPluginsFormVersion}/jquery.form.js", jQueryPluginsFormJsFile)

		String text = jQueryPluginsFormJsFile.text

		text = text.replace("typeof define === 'function'", "false && typeof define === 'function'")

		text = ";(function(jQuery){" + System.getProperty("line.separator") + text + '})(AUI.$);'

		jQueryPluginsFormJsFile.text = text
	}

	onlyIf {
		!jQueryPluginsFormJsFile.exists()
	}
}

buildLexicon {
	File lexiconAssetsDestinationDir = new File(jsDestinationDir, "lexicon-assets")
	File lexiconDestinationDir = new File(jsDestinationDir, "lexicon")
	File lexiconZipFile = new File(thirdPartyDir, "lexicon-${lexiconVersion}.zip")

	doFirst {
		delete lexiconAssetsDestinationDir, lexiconDestinationDir
	}

	eachFile new ReplaceLeadingPathAction("js": lexiconDestinationDir.name, "": lexiconAssetsDestinationDir.name)
	from zipTree(lexiconZipFile)
	includeEmptyDirs = false
	into jsDestinationDir

	onlyIf {
		!FileUtil.isUpToDate(project, lexiconZipFile, lexiconDestinationDir)
	}
}

buildLfrAMDLoader {
	dependsOn downloadLfrAMDLoader
	from new File(node.nodeDir, "node_modules/lfr-amd-loader/dist/loader.js")
	into new File(jsDestinationDir, "loader")
}

cache {
	task(buildLfrAMDLoader) {
		baseDir = buildLfrAMDLoader.destinationDir
		cacheDir = ".task-cache"
		include "loader.js"
		testFile "build.gradle"
	}
}

classes {
	dependsOn buildAlloy
	dependsOn buildAlloyBootstrap
	dependsOn buildJQueryPluginsForm
	dependsOn buildLexicon
	dependsOn buildLfrAMDLoader
}

dependencies {
	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
}

downloadLfrAMDLoader {
	moduleName = "lfr-amd-loader"
	moduleVersion = lfrAMDLoaderVersion
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}