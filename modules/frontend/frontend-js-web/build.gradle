import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.ReplaceLeadingPathAction
import com.liferay.gradle.util.copy.StripPathSegmentsAction

configurations {
	jQuery
	lfrAMDLoader
	lodash
	svg4everybody
}

task buildAlloy(type: Copy)
task buildJQuery(type: Copy)
task buildJQueryPluginsForm
task buildLexicon(type: Copy)
task buildLfrAMDLoader(type: Copy)
task buildLodash(type: Copy)
task buildSvg4everybody(type: Copy)

String alloyVersion = "3.0"
String jqueryPluginsFormVersion = "3.51"
String jQueryVersion = "2.1.4"
String lexiconVersion = "0.1"
String lfrAMDLoaderVersion = "1.3.5"
String lodashVersion = "3.10.1"
String svg4everybodyVersion = "2.0.0"

File jsDestinationDir = file("tmp/META-INF/resources")
File thirdPartyDir = file("../../../portal-web/third-party")

buildAlloy {
	File alloyDestinationDir = new File(jsDestinationDir, "aui")
	FileTree alloyZipFileTree = fileTree(dir: thirdPartyDir, include: "alloy-${alloyVersion}*.zip")

	doFirst {
		delete alloyDestinationDir
	}

	doLast {
		File auiDeprecatedCssFile = new File(alloyDestinationDir, "aui_deprecated.css")

		File auiSkinDeprecatedCssFile = new File(alloyDestinationDir, "aui-skin-deprecated/css/aui-skin-deprecated.css")
		FileTree cssFileTree = fileTree(dir: alloyDestinationDir, include: "*-deprecated/assets/skins/sam/*-deprecated.css")

		FileUtil.concatenate(auiDeprecatedCssFile, files(auiSkinDeprecatedCssFile, cssFileTree))

		ant.replaceregexp(file: auiDeprecatedCssFile, flags: "g", match: "url\\(([^)]+)\\)", replace: "url(@theme_image_path@/aui/\\1)")

		ant.replace(file: auiDeprecatedCssFile) {
			replacefilter token: "../images/", value: ""
			replacefilter token: "../../../../../build/aui-skin-base/images/", value: ""
			replacefilter token: "../../../../../build/aui-skin-classic/images/", value: ""
			replacefilter token: "../../../../../build/aui-skin-deprecated/images/", value: ""
		}
	}

	eachFile new StripPathSegmentsAction(1)
	from zipTree(alloyZipFileTree.singleFile)
	include "build/**"
	includeEmptyDirs = false
	into alloyDestinationDir

	onlyIf {
		!FileUtil.isUpToDate(project, alloyZipFileTree.singleFile, alloyDestinationDir)
	}
}

buildJQuery {
	eachFile new StripPathSegmentsAction(6)

	from {
		zipTree(configurations.jQuery.singleFile)
	}

	include "META-INF/resources/webjars/jquery/${jQueryVersion}/dist/jquery.js"
	includeEmptyDirs = false
	into new File(jsDestinationDir, "jquery")
}

buildJQueryPluginsForm {
	File jQueryPluginsFormJsFile = file("src/main/resources/META-INF/resources/jquery/form.js")

	doLast {
		FileUtil.get(project, "https://raw.githubusercontent.com/malsup/form/${jqueryPluginsFormVersion}/jquery.form.js", jQueryPluginsFormJsFile)

		String text = jQueryPluginsFormJsFile.text

		text = text.replace("typeof define === 'function'", "false && typeof define === 'function'")

		text = ";(function(jQuery){" + System.getProperty("line.separator") + text + '})(AUI.$);'

		jQueryPluginsFormJsFile.text = text
	}

	onlyIf {
		!jQueryPluginsFormJsFile.exists()
	}
}

buildLexicon {
	File lexiconDestinationDir = new File(jsDestinationDir, "lexicon")
	FileTree lexiconZipFileTree = fileTree(dir: thirdPartyDir, include: "lexicon-${lexiconVersion}*.zip")

	doFirst {
		delete lexiconDestinationDir
	}

	eachFile new ReplaceLeadingPathAction("js": lexiconDestinationDir.name)
	from zipTree(lexiconZipFileTree.singleFile)
	includeEmptyDirs = false
	into jsDestinationDir

	onlyIf {
		!FileUtil.isUpToDate(project, lexiconZipFileTree.singleFile, lexiconDestinationDir)
	}
}

buildLfrAMDLoader {
	eachFile new StripPathSegmentsAction(6)

	from {
		zipTree(configurations.lfrAMDLoader.singleFile)
	}

	include "META-INF/resources/webjars/lfr-amd-loader/${lfrAMDLoaderVersion}/dist/loader.js"
	includeEmptyDirs = false
	into new File(jsDestinationDir, "loader")
}

buildLodash {
	eachFile new StripPathSegmentsAction(5)

	from {
		zipTree(configurations.lodash.singleFile)
	}

	include "META-INF/resources/webjars/lodash/${lodashVersion}/lodash.js"
	includeEmptyDirs = false
	into new File(jsDestinationDir, "lodash")
}

buildSvg4everybody {
	eachFile new StripPathSegmentsAction(6)

	filter {
		String line ->

		line.replace('"function" == typeof define', 'false && "function" == typeof define')
	}

	from {
		zipTree(configurations.svg4everybody.singleFile)
	}

	include "META-INF/resources/webjars/svg4everybody/${svg4everybodyVersion}/dist/svg4everybody.js"
	includeEmptyDirs = false
	into new File(jsDestinationDir, "misc")
}

classes {
	dependsOn buildAlloy
	dependsOn buildJQuery
	dependsOn buildJQueryPluginsForm
	dependsOn buildLexicon
	dependsOn buildLfrAMDLoader
	dependsOn buildLodash
	dependsOn buildSvg4everybody
}

dependencies {
	compile group: "com.liferay.portal", name: "portal-service", version: liferay.portalVersion
	compile group: "com.liferay.portal", name: "util-taglib", version: liferay.portalVersion
	compile group: "javax.portlet", name: "portlet-api", version: "2.0"
	compile group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compile group: "javax.servlet.jsp", name: "jsp-api", version: "2.1"
	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"

	jQuery group: "org.webjars.npm", name: "jquery", transitive: false, version: jQueryVersion

	lfrAMDLoader group: "org.webjars.npm", name: "lfr-amd-loader", transitive: false, version: lfrAMDLoaderVersion

	lodash group: "org.webjars.bower", name: "lodash", transitive: false, version: lodashVersion

	svg4everybody group: "org.webjars.npm", name: "svg4everybody", transitive: false, version: svg4everybodyVersion
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}