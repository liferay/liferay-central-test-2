import com.liferay.gradle.util.copy.StripPathSegmentsAction

dependencies {
	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
}

ext {
	bourbonVersion = "3.2.3"
}

liferay {
	deployDir = file("${liferayHome}/osgi/portal")
}

task buildAlloyTheme(type: Copy)
task buildBourbon(type: Copy)

buildAlloyTheme {
	dependsOn ":frontend:frontend-js-web:buildAlloy"

	ext {
		autoClean = false
	}

	from "../frontend-js-web/tmp/META-INF/resources/html/js/aui/aui_deprecated.css"
	into "src/META-INF/resources/html/css/portal"
}

buildBourbon {
	eachFile new StripPathSegmentsAction(4)

	ext {
		autoClean = false
	}

	from zipTree("../../../portal-web/third-party/bourbon-${bourbonVersion}.zip")
	include "bourbon-${bourbonVersion}/app/assets/stylesheets/**/*"
	includeEmptyDirs = false
	into "../../../portal-web/docroot/html/css/common/bourbon"
}

classes {
	dependsOn buildAlloyTheme
}

jar {
	dependsOn buildBourbon
}