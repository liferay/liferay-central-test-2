import com.liferay.gradle.plugins.node.tasks.DownloadNodeModuleTask
import com.liferay.gradle.plugins.node.tasks.ExecuteNodeTask
import com.liferay.gradle.plugins.node.tasks.ExecuteNpmTask

dependencies {
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
}

ext {
	bowerVersion = "1.5.2"
	gulpVersion = "3.9.0"
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}

task downloadBower(type: DownloadNodeModuleTask)
task downloadGulp(type: DownloadNodeModuleTask)
task npmInstall(type: ExecuteNpmTask)
task runGulp(type: ExecuteNodeTask)
task bowerInstall(type: ExecuteNodeTask)

bowerInstall {
	args = [new File(node.nodeDir, "node_modules/bower/bin/bower"), "install", "--allow-root"]
	dependsOn npmInstall
}

configJSModules {
	dependsOn runGulp
	include "**/*.js"
}

downloadBower {
	moduleName = "bower"
	moduleVersion = bowerVersion
}

downloadGulp {
	moduleName = "gulp"
	moduleVersion = gulpVersion
}

npmInstall {
	args "install"

	dependsOn downloadBower
	dependsOn downloadGulp
}

runGulp {
	args = [new File(node.nodeDir, "node_modules/gulp/bin/gulp.js")]
	dependsOn bowerInstall
}