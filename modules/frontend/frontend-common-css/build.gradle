import com.liferay.gradle.plugins.node.tasks.PublishNodeModuleTask
import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

task buildBourbon(type: Copy)
task buildCssCommon(type: Copy)
task publishNodeModule(type: PublishNodeModuleTask)

File commonCssDestinationDir = file("tmp/META-INF/resources")

buildBourbon {
	File bourbonDestinationDir = new File(commonCssDestinationDir, "bourbon")

	from("src/main/resources/META-INF/resources") {
		exclude "**.npmignore"
		exclude "**.json"
	}

	includeEmptyDirs = false
	into commonCssDestinationDir

	into(bourbonDestinationDir.name) {
		eachFile new StripPathSegmentsAction(8)
		from ({ zipTree(configurations.compile.fileCollection { it.name.startsWith("bourbon") }.singleFile) })
		include "**/app/assets/stylesheets/**/*"
		includeEmptyDirs = false
	}
}

buildCssCommon {
	dependsOn buildBourbon
	from commonCssDestinationDir
	into new File(liferay.appServerPortalDir, "html/css/common")
}

classes {
	dependsOn buildCssCommon
}

dependencies {
	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	compile group: "org.webjars.bower", name: "bourbon", version: "4.2.3"
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}

publishNodeModule {
	workingDir = "src/main/resources/META-INF/resources"
}