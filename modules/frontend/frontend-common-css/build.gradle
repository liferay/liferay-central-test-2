import com.liferay.gradle.plugins.node.tasks.PublishNodeModuleTask
import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.ReplaceLeadingPathAction

task buildBourbon(type: Copy)
task buildCssCommon(type: Copy)
task publishNodeModule(type: PublishNodeModuleTask)

String bourbonVersion = "3.2.3"

File commonCssDestinationDir = file("tmp/META-INF/resources")

buildBourbon {
	File bourbonDestinationDir = new File(commonCssDestinationDir, "bourbon")
	File bourbonZipFile = file("../../../portal-web/third-party/bourbon-${bourbonVersion}.zip")

	from("src/main/resources/META-INF/resources") {
		exclude "**.npmignore"
		exclude "**.json"
	}

	includeEmptyDirs = false
	into commonCssDestinationDir

	into(bourbonDestinationDir.name) {
		eachFile new ReplaceLeadingPathAction("bourbon/bourbon-3.2.3/app/assets/stylesheets": "bourbon")
		from zipTree(bourbonZipFile)
		include "bourbon-${bourbonVersion}/app/assets/stylesheets/**/*"
	}

	onlyIf {
		!FileUtil.isUpToDate(project, bourbonZipFile, bourbonDestinationDir)
	}
}

buildCssCommon {
	dependsOn buildBourbon
	from commonCssDestinationDir
	into new File(liferay.appServerPortalDir, "html/css/common")
}

classes {
	dependsOn buildCssCommon
}

dependencies {
	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}

publishNodeModule {
	workingDir = "src/main/resources/META-INF/resources"
}