import java.util.jar.Attributes
import java.util.jar.JarFile

task writeReleases

dependencies {
	compile group: "com.liferay", name: "com.liferay.blogs.api", version: "3.0.0"
	compile group: "com.liferay", name: "com.liferay.blogs.web", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.document.library.api", version: "3.0.0"
	compile group: "com.liferay", name: "com.liferay.document.library.web", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.lists.api", version: "2.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.lists.service", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.lists.web", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.mapping.api", version: "3.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.mapping.service", version: "2.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.mapping.web", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.journal.api", version: "2.0.0"
	compile group: "com.liferay", name: "com.liferay.journal.service", version: "3.0.0"
	compile group: "com.liferay", name: "com.liferay.login.web", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.message.boards.api", version: "3.0.0"
	compile group: "com.liferay", name: "com.liferay.message.boards.web", version: "1.0.0"
	compile group: "com.liferay.portal", name: "com.liferay.util.java", version: "2.0.0"
	compile group: "javax.portlet", name: "portlet-api", version: "2.0"
	compile project(":apps:collaboration:wiki:wiki-api")
	compile project(":apps:collaboration:wiki:wiki-service")

	provided group: "com.liferay", name: "org.freemarker", version: "2.3.23.LIFERAY-PATCHED-3"
	provided group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
}

classes {
	dependsOn writeReleases
}

writeReleases {
	doLast {
		File releasesTxtFile = new File(processResources.destinationDir, "com/liferay/portal/tools/sample/sql/builder/dependencies/releases.txt")

		releasesTxtFile.withWriter("UTF-8") {
			Writer writer ->

			configurations.compile.each {
				JarFile jarFile = new JarFile(it)

				Attributes mainAttributes = jarFile.manifest.mainAttributes

				String schemaVersion = mainAttributes.getValue("Liferay-Require-SchemaVersion")

				if (!schemaVersion) {
					return
				}

				String symbolicName = mainAttributes.getValue("Bundle-SymbolicName")

				writer.writeLine "${symbolicName}:${schemaVersion}"
			}
		}
	}
}