import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

dependencies {
	compile group: "com.liferay", name: "com.liferay.sass.compiler.api", version: "1.0.0"
	compile group: "org.jruby", name: "jruby-complete", version: "1.7.20"

	provided group: "com.liferay", name: "com.liferay.ruby.gems", transitive: false, version: "1.0.0-SNAPSHOT"

	testCompile group: "junit", name: "junit", version: "4.12"
}

ext {
	sassSpecVersion = "3818b9d686765461add05e996d990b085e15a348"

	sassSpecUrl = "https://github.com/sass/sass-spec/archive/${sassSpecVersion}.zip"
}

task downloadSassSpec(type: Copy)

downloadSassSpec {
	File sassSpecFile = project.file("test/unit/com/liferay/sass/compiler/ruby/internal/dependencies/sass-spec")

	eachFile new StripPathSegmentsAction(3)

	from zipTree(
		{
			FileUtil.get(project, sassSpecUrl)
		})

	include "**/spec/basic/**"
	into sassSpecFile

	onlyIf {
		!sassSpecFile.exists()
	}
}

processTestResources {
	dependsOn downloadSassSpec
}