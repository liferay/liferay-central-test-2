apply plugin: "com.liferay.maven.plugin.builder"

buildPluginDescriptor {
	ext {
		autoClean = false
	}

	finalizedBy formatSource
	finalizedBy formatXML

	goalPrefix = "bundle-support"
	useSetterComments = false
}

configurations {
	mavenDistribution
}

dependencies {
	compileInclude group: "com.beust", name: "jcommander", version: "1.48"
	compileInclude group: "commons-codec", name: "commons-codec", version: "1.10"
	compileInclude group: "commons-logging", name: "commons-logging", version: "1.2"
	compileInclude group: "org.apache.commons", name: "commons-compress", version: "1.12"
	compileInclude group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.2"
	compileInclude group: "org.apache.httpcomponents", name: "httpcore", version: "4.4.4"

	mavenDistribution classifier: "bin", ext: "zip", group: "org.apache.maven", name: "apache-maven", version: "3.3.9"

	provided group: "org.apache.ant", name: "ant", transitive: false, version: "1.9.7"
	provided group: "org.apache.maven", name: "maven-core", transitive: false, version: "3.2.5"
	provided group: "org.apache.maven", name: "maven-model", transitive: false, version: "3.2.5"
	provided group: "org.apache.maven", name: "maven-plugin-api", transitive: false, version: "3.2.5"
	provided group: "org.apache.maven", name: "maven-settings", transitive: false, version: "3.2.5"
	provided group: "org.apache.maven.plugin-tools", name: "maven-plugin-annotations", transitive: false, version: "3.5"

	testCompile group: "com.sun.net.httpserver", name: "http", version: "20070405"
	testCompile group: "org.apache.ant", name: "ant-testutil", version: "1.9.7"
	testCompile group: "org.littleshoot", name: "littleproxy", version: "1.1.1"
	testCompile project(":test:maven-executor")
}

formatSource {
	mustRunAfter formatXML
}

liferayOSGi {
	expandCompileInclude = true
}

test {
	dependsOn install

	doFirst {
		systemProperty "maven.distribution.file.name", configurations.mavenDistribution.singleFile
	}

	systemProperty "http.nonProxyHosts", "localhost.localdomain"
	systemProperty "http.proxyHost", System.properties["http.proxyHost"]
	systemProperty "http.proxyPort", System.properties["http.proxyPort"]
	systemProperty "maven.debug", System.properties["maven.debug"]
	systemProperty "maven.repo.local", System.properties["maven.repo.local"]
	systemProperty "repository.url", System.properties["repository.url"]
	systemProperty "bundle.support.version", jar.version
}

updateFileVersions {
	match(/\t\t\t\t<version>(\d.+)<\/version>/, "samples/pom.xml")
}