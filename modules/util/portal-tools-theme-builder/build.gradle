apply plugin: "com.liferay.maven.plugin.builder"

task writeParentThemeDependencies

buildPluginDescriptor {
	ext {
		autoClean = false
	}

	finalizedBy formatSource
	finalizedBy formatXML

	goalPrefix = "liferay"
}

configurations {
	parentThemes
}

dependencies {
	parentThemes project(":apps:foundation:frontend-theme:frontend-theme-styled")
	parentThemes project(":apps:foundation:frontend-theme:frontend-theme-unstyled")

	provided group: "commons-cli", name: "commons-cli", transitive: false, version: "1.3.1"
	provided group: "net.coobird", name: "thumbnailator", transitive: false, version: "0.4.8"
	provided group: "org.apache.ant", name: "ant", transitive: false, version: "1.9.4"
	provided group: "org.apache.maven", name: "maven-plugin-api", transitive: false, version: "3.0.4"
}

formatSource {
	mustRunAfter formatXML
}

liferay {
	deployDir = "../../../tools/sdk/dependencies/com.liferay.portal.tools.theme.builder/lib"
}

test {
	dependsOn writeParentThemeDependencies
}

writeParentThemeDependencies {
	configurations.parentThemes.dependencies.withType(ProjectDependency) {
		dependsOn "${dependencyProject.path}:jar"
	}

	doLast {
		File parentThemeDependenciesFile = new File(buildDir, "parent-theme-dependencies.txt")

		parentThemeDependenciesFile.parentFile.mkdirs()

		parentThemeDependenciesFile.text = configurations.parentThemes.join(System.lineSeparator())
	}
}