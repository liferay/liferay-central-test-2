import com.liferay.gradle.util.FileUtil

configurations {
	jruby
}

ext {
	compassVersion = "1.0.1"

	compassUrl = "http://rubygems.org/gems/compass-${compassVersion}.gem"
}

dependencies {
	jruby group: "org.jruby", name: "jruby-complete", version: "1.6.5"
}

task downloadGems

classes {
	dependsOn downloadGems
}

downloadGems {
	doLast {
		File compassFile = FileUtil.get(project, compassUrl, temporaryDir)

		javaexec {
			args configurations.jruby.singleFile
			args "-S"
			args "gem", "install"
			args "-i", "./classes"
			args relativePath(compassFile)
			args "--no-rdoc"
			args "--no-ri"

			main = "-jar"
		}

		ant.replace(dir: "classes/bin") {
			replacetoken "#! jruby"
			replacevalue "#!/usr/bin/env jruby"
		}
	}

	onlyIf {
		!FileUtil.exists(project, "classes/gems")
	}
}