import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.OSDetector

import groovy.io.FileType

dependencies {
	compile group: "joda-time", name: "joda-time", version: "1.5.2"
}

ext {
	srcUrl = "http://google-rfc-2445.googlecode.com/svn/trunk/src@36"
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

task downloadSrc(type: Exec)

compileJava {
	dependsOn downloadSrc
}

downloadSrc {
	File srcTmpDir = new File(temporaryDir, "src")

	if (OSDetector.windows) {
		executable "cmd.exe"

		args "/c"
	}
	else {
		executable "/bin/sh"

		args "-c"
	}

	args "\"svn export ${srcUrl} ${relativePath(srcTmpDir)}\""

	doFirst {
		delete srcTmpDir
		delete "src/main/java"
		delete "src/main/resources"
	}

	doLast {
		srcTmpDir.eachFileRecurse(FileType.FILES) {
			String dirName = "resources"

			if (it.name.endsWith(".java")) {
				dirName = "java"
			}

			File dir = file("src/main/" + dirName)

			File destFile = new File(dir, FileUtil.relativize(it, srcTmpDir))

			destFile.parentFile.mkdirs()

			it.renameTo(destFile)
		}
	}

	onlyIf {
		!FileUtil.exists(project, "src/main/java")
	}
}