import com.liferay.gradle.util.FileUtil

dependencies {
	compile fileTree(dir: "lib", include: "*.jar")
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

task downloadSrc(type: Copy)

clean {
	delete "lib"
	delete "src/main/java"
	delete "src/main/resources"
}

compileJava {
	dependsOn downloadSrc
}

downloadSrc {
	eachFile {
		if (path.startsWith("gdata/java/lib") || path.startsWith("gdata/java/sample/util/lib")) {
			path = "lib/" + name
		}
		else {
			String dirName = "resources"

			if (name.endsWith(".java")) {
				dirName = "java"

				filter {
					String line ->

					line.replace("sample.appsforyourdomain", "com.google.gdata.sample.appsforyourdomain")
				}
			}

			path = "src/main/${dirName}/com/google/gdata/${path[11 .. (path.length() - 1)]}"
		}
	}

	ext {
		autoClean = false
	}

	from {
		zipTree FileUtil.get(project, "https://gdata-java-client.googlecode.com/files/gdata-samples.java-1.47.1.zip")
	}

	include "gdata/java/lib/gdata-appsforyourdomain-1.0.jar"
	include "gdata/java/lib/gdata-client-1.0.jar"
	include "gdata/java/lib/gdata-core-1.0.jar"
	include "gdata/java/lib/gdata-media-1.0.jar"
	include "gdata/java/sample/appsforyourdomain/**"
	include "gdata/java/sample/util/lib/sample-util.jar"

	includeEmptyDirs = false

	into projectDir

	onlyIf {
		!FileUtil.exists(project, "src/main/java")
	}
}