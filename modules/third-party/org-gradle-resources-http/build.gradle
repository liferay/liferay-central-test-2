import com.liferay.gradle.util.FileUtil

task deploy(type: Zip, overwrite: true)
task jar(type: Jar, overwrite: true)

String gradleVersion = version.substring(0, version.lastIndexOf('.'))

if (gradleVersion != gradle.gradleVersion) {
	throw new GradleException("Unable to use Gradle " + gradle.gradleVersion + ", " + gradleVersion + " is required")
}

dependencies {
	compile gradleApi()
}

deploy {
	baseName = "gradle"
	classifier = "bin"
	destinationDir = rootProject.file("../tools")
	exclude "gradle-${gradleVersion}/lib/plugins/gradle-resources-http-${gradleVersion}.jar"

	ext {
		autoClean = false
	}

	from {
		zipTree(FileUtil.get(project, "https://services.gradle.org/distributions/gradle-${gradleVersion}-bin.zip"))
	}

	into("gradle-${gradleVersion}/lib/plugins") {
		from jar
	}
}

jar {
	baseName = "gradle-resources-http"
	duplicatesStrategy = "exclude"
	from sourceSets.main.output

	from zipTree(
		{
			configurations.compile.find {
				it.name.startsWith "gradle-resources-http-"
			}
		})
}