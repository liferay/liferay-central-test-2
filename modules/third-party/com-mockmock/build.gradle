import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.OSDetector

ext {
	mockMockGitHash = "1e88530f74aea4efa139fec7214f395d25a4032b"
	mockMockSrcUrl = "https://github.com/tweakers-dev/MockMock/archive/${mockMockGitHash}.zip"
	mockMockSrcVersion = "1.3"
}

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

File srcDir = new File(buildDir, "MockMock-${mockMockGitHash}")

task compileJavaMvn(type: Exec)
task downloadSrc(type: Copy)
task jarMvn(type: Exec)

compileJava {
	deleteAllActions()
	dependsOn compileJavaMvn
}

compileJavaMvn {
	commandLine _getMvnCommand("compile")
	dependsOn downloadSrc
	workingDir = srcDir
}

downloadSrc {
	from {
		zipTree FileUtil.get(project, mockMockSrcUrl)
	}

	into buildDir
}

jar {
	archiveName = "com.liferay.com.mockmock.${mockMockSrcVersion}.jar"
	deleteAllActions()
	dependsOn jarMvn
	version = mockMockSrcVersion
}

jarMvn {
	commandLine _getMvnCommand("package")
	dependsOn compileJavaMvn

	doLast {
		copy {
			from new File(srcDir, "target")
			include "MockMock-${mockMockSrcVersion}.one-jar.jar"
			into distsDir
			rename ".*", jar.archiveName
		}
	}

	workingDir = srcDir
}

private List<String> _getMvnCommand(String command) {
	if (OSDetector.windows) {
		return ["cmd.exe", "/c", "mvn ${command}"]
	}

	return ["/bin/sh", "-c", "mvn ${command}"]
}